#version 430 core

#ifndef CUSTOM_DEFINES
#define WORK_GROUP_SIZE_X 1
#define WORK_GROUP_SIZE_Y 1
#define WORK_GROUP_SIZE_Z 1
#endif

layout (local_size_x = WORK_GROUP_SIZE_X, local_size_y = WORK_GROUP_SIZE_Y, local_size_z = WORK_GROUP_SIZE_Z) in;

layout(r32f, binding = 0) uniform image2D prev_phi_map;
layout(r32f, binding = 1) uniform image2D prev_T_map;

layout(r32f, binding = 2) uniform image2D next_phi_map;
layout(r32f, binding = 3) uniform image2D next_T_map;

uniform int _SIZE_X;
uniform int _SIZE_Y;

uniform int _INITIAL_SIZE_X;
uniform int _INITIAL_SIZE_Y;
uniform int _frame_i;
uniform float _INITIAL_PHI;
uniform float _INITIAL_T;
uniform float _AROUND_PHI;
uniform float _AROUND_T;

uniform float _dt;
uniform float _alpha;
uniform float _L;
uniform float _xi;
uniform float _a;
uniform float _b;
uniform float _beta;
uniform float _Tm;

#define mK		1
#define tau		1

float reaction_term_0(float phi)
{
	return phi*(1 - phi)*(phi - 1.0/2);
}

float reaction_term_1(float phi, float T, float xi)
{
	return _a*reaction_term_0(phi) - _b*_beta*xi*(T - _Tm);
}

float phi_at(int x, int y)
{
	y = y % _SIZE_Y;
	x = x % _SIZE_X;
	return imageLoad(prev_phi_map, ivec2(x, y)).r;
}

float T_at(int x, int y)
{
	y = y % _SIZE_Y;
	x = x % _SIZE_X;
	return imageLoad(prev_T_map, ivec2(x, y)).r;
}

void main() 
{
	ivec2 center = ivec2(gl_GlobalInvocationID.xy);
	int x = center.x;
	int y = center.y;
	
	//Initial conditions
	if(_frame_i == 0)
	{
		float T = _AROUND_T;
		float phi = _AROUND_PHI;

		int lower_x = (_SIZE_X - _INITIAL_SIZE_X) / 2;
		int upper_x = (_SIZE_X + _INITIAL_SIZE_X) / 2;
		
		int lower_y = (_SIZE_Y - _INITIAL_SIZE_Y) / 2;
		int upper_y = (_SIZE_Y + _INITIAL_SIZE_Y) / 2;
		if((lower_x <= x && x <= upper_x) && 
			(lower_y <= y && y <= upper_y))
		{
			T = _INITIAL_T;
			phi = _INITIAL_PHI;
		}
		
		imageStore(next_phi_map, center, vec4(phi, 0, 0, 0));
		imageStore(next_T_map, center, vec4(T, 0, 0, 0));
		return;
	}

	float T = T_at(x, y);
	float phi = phi_at(x, y);
	
    float phi_py = phi_at(x, y + 1);
    float phi_my = phi_at(x, y - 1);
    float phi_px = phi_at(x + 1, y);
    float phi_mx = phi_at(x - 1, y);

    float T_py = T_at(x, y + 1);
    float T_my = T_at(x, y - 1);
    float T_px = T_at(x + 1, y);
    float T_mx = T_at(x - 1, y);

	float sum_phi_neigbours = 0
		+ tau*(phi_py - phi)
		+ tau*(phi_my - phi)
		+ tau*(phi_px - phi)
		+ tau*(phi_mx - phi);
		
	float sum_T_neigbours = 0
		+ tau*(T_py - T)
		+ tau*(T_my - T)
		+ tau*(T_px - T)
		+ tau*(T_mx - T);

	
	float reaction_term = reaction_term_1(phi, T, _xi);
	float phi_dt = (sum_phi_neigbours/mK + reaction_term/(_xi*_xi)) / _alpha;
	float T_dt = sum_T_neigbours / mK + _L * phi_dt;

	float phi_next = phi_dt * _dt + phi;
	float T_next = T_dt * _dt + T;

	imageStore(next_phi_map, center, vec4(phi_next, 0, 0, 0));
	imageStore(next_T_map, center, vec4(T_next, 0, 0, 0));
}